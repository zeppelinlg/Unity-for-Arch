# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=vivid
# vercheck-launchpad: name=${pkgname}

pkgname=unity-lens-video
_actual_ver=0.3.15
_extra_ver=+13.10.20130920
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="Unity Video lens"
arch=(i686 x86_64)
url="https://launchpad.net/unity-lens-videos"
license=(GPL)
depends=(dee json-glib libgee06 libsoup-gnome libunity libzeitgeist
         unity-lens-music)
makedepends=(gnome-common intltool vala)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-video_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('8afe4c92d3c85345c9a6e448227b775305afd9605af7ad147a43bec3485334a1b15c4b2ee0620230d25866bf6712d2fc5507cf2a6e90818fa896dca1295913b8')

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib --enable-headless-tests
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}
